plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
}

group 'truffleclr'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    systemProperty "truffle.class.path.append", "./language/build/libs/language-1.0.0.jar"
    workingDir ".."
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation group: 'org.graalvm.truffle', name: 'truffle-api', version: '20.0.0'
    implementation "com.github.ajalt:clikt:2.6.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.6.0"

}

task fatJar(type: Jar) {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes(
                'Built-By'       : System.properties['user.name'],
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Main-Class'     : "AppKt"
        )
    }
    with jar
}

task copyJar(type: Copy, dependsOn: [ fatJar ]) {
    from fatJar
    into "../build/libs"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}